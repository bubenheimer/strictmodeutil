/*
 * Copyright (c) 2015-2016 Uli Bubenheimer.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

buildscript {
    repositories {
        mavenLocal()
        google()
        gradlePluginPortal()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.0-alpha02'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.8'
    }
}

group = 'org.bubenheimer'
version = '1.0-SNAPSHOT'

ext {
    minSdkVersion = 14
    compileSdkVersion = 28
    targetSdkVersion = 28
    javaVersion = JavaVersion.VERSION_1_8
}

subprojects {
    buildscript {
        repositories {
            mavenLocal()
            google()
        }
    }

    repositories {
        mavenLocal()
        google()
        jcenter()
    }

    apply plugin: 'com.android.library'
    apply plugin: 'com.github.dcendents.android-maven'
    apply plugin: 'net.ltgt.errorprone'

    group = rootProject.group
    version = rootProject.version

    android {
        compileSdkVersion rootProject.ext.compileSdkVersion

        defaultConfig {
            minSdkVersion rootProject.ext.minSdkVersion
            targetSdkVersion rootProject.ext.targetSdkVersion
            versionName version
        }

        buildTypes {
            debug {
            }
            release {
                shrinkResources false
                debuggable false
                jniDebuggable false
                renderscriptDebuggable false
            }
        }

        compileOptions {
            sourceCompatibility rootProject.ext.javaVersion
            targetCompatibility rootProject.ext.javaVersion
        }

        // TODO change based on resolution of https://issuetracker.google.com/issues/72050365
        libraryVariants.all {
            it.generateBuildConfig.enabled = false
        }
    }

    dependencies {
        errorprone 'com.google.errorprone:error_prone_core:2.3.3'
        //noinspection GradleDynamicVersion
        errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'

        implementation 'org.bubenheimer:javautil:1.0-SNAPSHOT'
    }
}
