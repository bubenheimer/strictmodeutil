/*
 * Copyright (c) 2015-2023 Uli Bubenheimer. All rights reserved.
 */

plugins {
    id('com.android.library')
    id("org.jetbrains.kotlin.android")
    id("maven-publish")
}

group = 'org.bubenheimer'
version = '1.0-SNAPSHOT'

android {
    compileSdk libs.versions.android.sdk.compile.get().toInteger()

    defaultConfig {
        minSdk libs.versions.android.sdk.min.get().toInteger()
        targetSdk libs.versions.android.sdk.target.get().toInteger()
        versionName version
    }

    buildTypes {
        debug {
        }
        release {
            shrinkResources false
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
        }
    }

    compileOptions {
        sourceCompatibility libs.versions.java.get()
        targetCompatibility libs.versions.java.get()
    }

    kotlinOptions {
        jvmTarget = libs.versions.java.get()
        freeCompilerArgs = [
                "-progressive",
                "-Xexplicit-api=strict",
                "-Xjvm-default=all"
        ]
    }

    buildFeatures {
        buildConfig false
    }

    publishing {
        multipleVariants {
            allVariants()
            withSourcesJar()
        }
    }

    lint {
        quiet false
        abortOnError false
        checkReleaseBuilds true
        ignoreWarnings false
        absolutePaths true
        checkAllWarnings true
        warningsAsErrors true
        // if true, don't include source code lines in the error output
        noLines false
        // if true, show all locations for an error, do not truncate lists, etc.
        showAll true
        // whether lint should include full issue explanations in the text error output
        explainIssues true
        textReport false
        xmlReport false
        htmlReport true
        // optional path to HTML report (default will be lint-results.html in the builddir)
        //htmlOutput file("$reportsDir/lint-report.html")
        checkTestSources true
        ignoreTestSources false
        checkGeneratedSources false
        checkDependencies false
    }
}

publishing {
    publications {
        library(MavenPublication) {
            afterEvaluate {
                from components.default
            }
        }
    }
}
